/**
 *
 * Copyright 2013-2015 Martin Goellnitz
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 *
 */
import groovy.util.Node;

defaultTasks 'clean', 'build'

println "Gradle: ${gradle.gradleVersion}"

version = '0.4-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenLocal()
  mavenCentral()
}

// Code coverage reporting
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

dependencies {
  compile "org.slf4j:slf4j-api:1.7.10"
  compile "commons-lang:commons-lang:2.6"
  compile "javax.inject:javax.inject:1"
  
  compile "javax.servlet:servlet-api:2.5"
    
  testCompile "junit:junit:4.12"
  // integrate tck into unit tests
  testCompile "javax.inject:javax.inject-tck:1"
  // tests are logging at debug level through special log4j.properties
  testCompile "org.slf4j:slf4j-log4j12:1.7.9"
  // Some servlet API elements
  testCompile "org.mockito:mockito-all:1.10.8" // 1.10.14 available from jcenter()
}

// Triggers some unit test elements
tasks.withType(Test) {
  systemProperty 'firstUrl', 'http://user:password@github.com:80/'
  systemProperty 'secondUrl', 'http://:password@github.com'
  systemProperty 'thirdUrl', '://:80'
}

// maven artifact publication stuff
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from "${buildDir}/docs/javadoc"
}


artifacts {
    archives javadocJar, sourcesJar
}

//signing {
//    sign configurations.archives
//}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
        }
    }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom.withXml {
        def pom = asNode()
        def first = pom.children().get(0)
        first.plus {
          new Node(pom, 'name', 'Dinistiq')
          new Node(pom, 'description', 'JSR 330 based Java  minimal footprint Dependency Injection framework')
          new Node(pom, 'url', 'https://github.com/mgoellnitz/dinistiq/')
          def scm = new Node(pom, 'scm')
          scm.appendNode('connection', 'scm:git:git@github.com:mgoellnitz/dinistiq.git')
          scm.appendNode('developerConnection', 'scm:git:git@github.com:mgoellnitz/dinistiq.git')
          scm.appendNode('url', 'git@github.com:mgoellnitz/dinistiq.git')
          def license = new Node(pom, 'licenses').appendNode('license')
          license.appendNode('name', 'LGPL-3.0')
          license.appendNode('url', 'http://opensource.org/licenses/LGPL-3.0')
          license
        }
        def developer = pom.appendNode('developers').appendNode('developer')
        developer.appendNode('name', 'Martin Goellnitz')
        developer.appendNode('organization', 'Provocon')
        developer.appendNode('organizationUrl', 'http://www.provocon.de/')
      }  
      groupId 'dinistiq'
      from components.java
      artifact sourcesJar 
      artifact javadocJar 
    }
  }
}
